buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.ch.myniva.gradle:s3-build-cache:0.4.1"
  }
}

apply plugin: 'ch.myniva.s3-build-cache'

ext.isMasterBuild = System.getenv("CIRCLE_BRANCH") == "master"

buildCache {
    local {
        enabled = !isMasterBuild
    }

    remote(ch.myniva.gradle.caching.s3.AwsS3BuildCache) {
        region = 'us-east-1'
        bucket = 'testcontainers-build-cache'
        push = isMasterBuild
    }
}

rootProject.name = 'testcontainers-java'

include "bom"

include "testcontainers"
project(':testcontainers').projectDir = "$rootDir/core" as File

file('modules').eachDir { dir ->
    include dir.name
    project(":${dir.name}").projectDir = dir
}